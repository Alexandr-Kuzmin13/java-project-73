plugins {
  id 'java'
  id 'application'
  id 'checkstyle'
  id 'org.springframework.boot' version '2.5.5'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'com.adarshr.test-logger' version '3.0.0'
  id 'org.liquibase.gradle' version '2.0.4'
  id 'org.springdoc.openapi-gradle-plugin' version '1.5.0'
  id 'jacoco'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
  mavenCentral()
}

compileJava {
  options.release = 17
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.liquibase:liquibase-core'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  runtimeOnly 'com.h2database:h2:1.4.200'
  runtimeOnly 'org.postgresql:postgresql:42.5.0'

  liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
  liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
  liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
  liquibaseRuntime sourceSets.main.output

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
  useJUnitPlatform()
}

testlogger {
  showStandardStreams = true
}

application {
  mainClass = 'hexlet.code.App'
}

checkstyle {
  toolVersion '9.2.1'
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
  }
}

jacoco {
  toolVersion = "0.8.8"
}

diffChangeLog {
  dependsOn compileJava
}

liquibase {
  activities {
    main {
      changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
      url 'jdbc:h2:./java_project_73'
      username 'sa'
      password ''
      referenceUrl 'hibernate:spring:hexlet.code.model' +
              '?dialect=org.hibernate.dialect.H2Dialect' +
              '&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
    }
  }
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
